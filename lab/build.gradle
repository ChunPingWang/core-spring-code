buildscript {
    ext {
        springBootVersion = "2.6.4"
        easyMockVersion = "4.1"
        jmonVersion = "2.82"
    }

    repositories {
        mavenCentral()
    }

}

plugins {
    id 'org.springframework.boot' version "$springBootVersion"
}

allprojects {
    apply plugin: 'maven-publish'

    group = 'io.pivotal.education.core-spring'
    version = '5.3.16'
}

subprojects {

    apply plugin: 'java'
    apply plugin: "io.spring.dependency-management"

    repositories {
        maven {
            url = 'https://repo.maven.apache.org/maven2'
        }
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
        }
    }

    dependencies {
        implementation "org.springframework.boot:spring-boot-starter"
        implementation "org.springframework.boot:spring-boot-starter-jdbc"
        implementation "org.hsqldb:hsqldb"

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    sourceCompatibility = '11'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
    }

    apply plugin: 'idea'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}
